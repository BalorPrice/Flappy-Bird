;-------------------------------------------------------
; MATHS ROUTINES

;-------------------------------------------------------
				ds align 256
maths.sin_table:
; 16-bit Sin table in vertical format
				db 0, 6, 13, 19, 25, 31, 38, 44, 50, 56, 62, 68, 74, 80, 86, 92, 98, 104, 109, 115, 121, 126, 132, 137, 142, 147, 152, 157, 162, 167, 172, 177, 181, 185, 190, 194, 198, 202, 206, 209, 213, 216, 220, 223, 226, 229, 231, 234, 237, 239, 241, 243, 245, 247, 248, 250, 251, 252, 253, 254, 255, 255, 0, 0, 0, 0, 0, 255, 255, 254, 253, 252, 251, 250, 248, 247, 245, 243, 241, 239, 237, 234, 231, 229, 226, 223, 220, 216, 213, 209, 206, 202, 198, 194, 190, 185, 181, 177, 172, 167, 162, 157, 152, 147, 142, 137, 132, 126, 121, 115, 109, 104, 98, 92, 86, 80, 74, 68, 62, 56, 50, 44, 38, 31, 25, 19, 13, 6, 0, 250, 243, 237, 231, 225, 218, 212, 206, 200, 194, 188, 182, 176, 170, 164, 158, 152, 147, 141, 135, 130, 124, 119, 114, 109, 104, 99, 94, 89, 84, 79, 75, 71, 66, 62, 58, 54, 50, 47, 43, 40, 36, 33, 30, 27, 25, 22, 19, 17, 15, 13, 11, 9, 8, 6, 5, 4, 3, 2, 1, 1, 0, 0, 0, 0, 0, 1, 1, 2, 3, 4, 5, 6, 8, 9, 11, 13, 15, 17, 19, 22, 25, 27, 30, 33, 36, 40, 43, 47, 50, 54, 58, 62, 66, 71, 75, 79, 84, 89, 94, 99, 104, 109, 114, 119, 124, 130, 135, 141, 147, 152, 158, 164, 170, 176, 182, 188, 194, 200, 206, 212, 218, 225, 231, 237, 243, 250

				db 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1, -1

;-------------------------------------------------------
maths.multADE:
; Input: A = Multiplier, DE = Multiplicand
; Output: A:HL = Product
				ld hl,0
				ld c,0

				add	a
				jr nc,@+skip
				ld h,d
				ld l,e
	@skip:

	@loop: equ for 6
				add hl,hl
				rla
				jr nc,$+4
				add hl,de
				adc c
	next @loop
				add hl,hl
				rla
				ret nc
				add hl,de
				adc c
				ret

;-------------------------------------------------------
; Pseudo-random number generator - by Jon Ritman, Simon Brattel, Neil Mottershead
maths.seed:		dm "C}_/"

maths.rand:				
				ld hl,(maths.seed + 2)
				ld d,l
				add hl,hl
				add hl,hl
				ld c,h
				ld hl,(maths.seed)
				ld b,h
				rl b
				ld e,h
				rl e
				rl d
				add hl,bc
				ld (maths.seed),hl
				ld hl,(maths.seed + 2)
				adc hl,de
				res 7,h
				ld (maths.seed + 2),hl
				jp m,@+skip
				ld hl,maths.seed
	@loop:
				inc (hl)
				inc hl
				jp z,@-loop
	@skip:	
				ld hl,(maths.seed)
				ret

;-------------------------------------------------------
maths.clampABC:
; Restrict A to between B and C.
	@minAC:
				cp c
				jp c,@+maxAB
				ld a,c
	@maxAB:
				cp b
				ret nc
				ld a,b
				ret

;-------------------------------------------------------
maths.modAB:
; Return A = A mod B
	@loop:
				sub b
				jr nc,@-loop
				add b
				ret

;-------------------------------------------------------
maths.absDE:
; return absolute value of DE
				bit 7,d
				ret z
				push hl
				ld hl,0
				and a
				sbc hl,de
				ex de,hl
				pop hl
				ret

;-------------------------------------------------------
maths.clampHBD:
; Restrict H to boundaries, upper of D, Lower of B, negative numbers allowed.
				ex af,af'
				ld a,h
	@minAD:
				cp d
				jp m,@+maxAB
				ld a,d
				ld l,0
	@maxAB:
				cp b
				jp p,@+end
				ld a,b
				ld l,0
	@end:
				ld h,a
				ex af,af'
				ret

;-------------------------------------------------------
